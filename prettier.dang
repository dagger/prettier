type Prettier {
  """
  The source directory for the project.
  """
  pub source: Directory! @defaultPath(path: "/")

  """
  The base image to use.
  """
  pub baseImageAddress: String! = "node:25-alpine"

  """
  Check that if the files are formatted.
  """
  pub check: Void @check {
    base.
    withExec(["npx", "prettier", "--check", "."]).
    sync

    null
  }

  """
  Rewrite all processed files in place.
  """
  pub write: Changeset {
    let fixFilter = ["**/*.js", "**/*.ts", "**/*.md", "**/*.jsx"]

    let fixed = base.
    withExec(["npx", "prettier", "--write", "."]).
    directory(".").
    withoutDirectory("node_modules")

    let fixedJs = directory.
    withDirectory(".", fixed, include: fixFilter)

    let sourceJs = directory.
    withDirectory(".", source.withoutDirectory("node_modules"), include: fixFilter)

    fixedJs.
    changes(sourceJs)
  }

  let base: Container! {
    container.from(baseImageAddress).
      withMountedCache("/root/.npm", cacheVolume("node-modules")).
      withDirectory("/src", source).
      withWorkdir("/src").
      withEnvVariable("CI", "true").
      withExec(["npm", "install"])
  }
}
